pkgname=st-git
_pkgname=st
pkgver=0.8.1.15.g30ec9a3
pkgrel=1
pkgdesc='Simple virtual terminal emulator for X'
url='http://st.suckless.org/'
arch=('i686' 'x86_64')
license=('MIT')
options=('zipman')
depends=('libxft' 'libxext' 'zsh' 'adobe-source-code-pro-fonts')
makedepends=('ncurses' 'libxext' 'git')
epoch=1
source=('git://git.suckless.org/st'
        'config.h'
        'https://st.suckless.org/patches/hidecursor/st-hidecursor-0.8.1.diff'
        'st-forcecommand-0.8.1.diff')
sha256sums=('SKIP'
            '4861b43590487a002b7a286654fbef40bbba23228beada83d4b438aed01e49b9'
            'bf3fe4e855f67fc9ae69b7328399ce06567f6aae3c9fb7fc8e7ec26c89e41dfd'
            'c634fe32cecd2743c3facd9f9221ec972486f544871bcbb77203a637cce965cc')

provides=("${_pkgname}")
conflicts=("${_pkgname}")

pkgver() {
  cd "${_pkgname}"
  git describe --tags | sed 's/-/./g'
}

prepare() {
  cd "${_pkgname}"
  # skip terminfo which conflicts with nsurses
  sed -i '/tic /d' Makefile
	for file in "${source[@]}"; do
		if [[ "$file" == "config.h" ]]; then
			# add config.h if present in source array
			# Note: this supersedes the above sed to config.def.h
			cp "$srcdir/$file" .
		elif [[ "$file" == *.diff || "$file" == *.patch ]]; then
			# add all patches present in source array
			patch -Np1 <"$srcdir/$(basename ${file})"
		fi
	done
}

build() {
  cd "${_pkgname}"
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd "${_pkgname}"
  make PREFIX=/usr DESTDIR="$pkgdir" TERMINFO="$pkgdir/usr/share/terminfo" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 README "$pkgdir/usr/share/doc/$pkgname/README"
}
